/**
 *
 * File:
 *   startup.ycp
 *
 * Module:
 *   CA Management
 *
 * Summary:
 *   Shows all available Root CAs
 *
 * Authors:
 *   Stefan Schubert <schubi@suse.de>
 *
 * $Id$
 *
 * CA Management - Root CAs
 *
 */


{

    textdomain "ca-management";

    import "CaMgm";
    import "Wizard";
    import "Label";
    import "Popup";
    import "YaPI::CaManagement";

    include "ca-management/util.ycp";    

    list<list> CAList = []; // CA list tree
    
    /**
     * Creates CA items
     * @param subCA - name ob the sub CA or empty if root
     * @return a list of CA items of the sub CA, formated for a UI tree
     */
    define list<term> getCAList (string subCA) ``{
        list<term> result = [];

	if (size (subCA) <= 0)
	{
	    CAList = (list<list>) YaPI::CaManagement::ReadCATree(); 
	    y2milestone("Output of ReadCATree: %1", CAList);
	    if (CAList == nil)
	    {
		showErrorCaManagement ();
		return result;
	    }
	}
	foreach (list elementList, CAList, ``{
	    if (elementList[1]:"" == subCA)
	    {
		list<term> subCA = getCAList(elementList[0]:"");
		if (size(subCA) > 0)
		{
		    result = add (result, `item (`id (elementList[0]:""), elementList[0]:"",true,
						 subCA));
		}
		else
		{
		    result = add (result, `item (`id (elementList[0]:""), elementList[0]:""));
		}
	    }
	});

        return result;
    }

    /**
     * Deleting current CA
     */
    define void deleteCurrentCA () ``{
	y2milestone("deleting CA: %1", CaMgm::currentCA );

	string message = sformat(_("Do you realy want to delete CA %1 ?"), CaMgm::currentCA );

	if (Popup::YesNoHeadline (_("Delete"),
				  message)
	    && getPassword(CaMgm::currentCA) != nil)
	{
	    if (YaPI::CaManagement::DeleteCA ($[ "caName":CaMgm::currentCA,
						 "caPasswd":getPassword(CaMgm::currentCA)]) == nil)
	    {
		map<string,any> messageMap = YaPI::Error();
		if (messageMap["code"]:"" == "CA_STILL_IN_USE")
		{
		    if (Popup::YesNoHeadline (_("Force Delete"),
					      _("This CA is still in use. Deleting ?")))
		    {
			if (YaPI::CaManagement::DeleteCA ($["caName":CaMgm::currentCA,
							    "caPasswd":getPassword(CaMgm::currentCA),
							    "force":"1"]) == nil)
			{
			    showErrorCaManagement ();			    			
			}
		    }
		}
		else
		{
		    showErrorCaManagement ();			    		    
		}
	    }
	}
     }


    /**
     * startup dialog
     * @return `finish, `enter, 'createRoot
     */
    define symbol Startup () ``{

        // help text 1/2
        string helptext = _("<p>Select one CA and press <b>Enter CA</b>.</p>");
        // help text 2/2
        helptext = helptext + _("<p><b>Create Root CA</b> generates a new root certificate authority.</p>");

        list<term> termList = getCAList ("");

        term buttons = `VBox ();
                                // To translators: pushbutton label
        buttons = add (buttons,  `HBox ( `HWeight (1, `PushButton (`id (`enter), `opt (`key_F4), _("&Enter CA")))));
        buttons = add (buttons,  `HBox ( `HWeight (1, `PushButton (`id (`delete), `opt (`key_F5), _("&Delete CA")))));
        buttons = add (buttons, `VStretch());
                                // To translators: pushbutton label
        buttons = add (buttons,  `HBox ( `HWeight(1, `PushButton (`id (`createRoot), `opt (`key_F3), _("&Create Root CA")))));
                                // To translators: pushbutton label	
        buttons = add (buttons,  `HBox ( `HWeight(1, `PushButton (`id (`import), _("&Import CA")))));	

        term contents = `HBox ();
        contents = add (contents, `HWeight (9,`Tree(`id(`tree), `opt(`notify, `vstretch),
                                           // To translators: tree headers
                                              _("CA Tree"),
                                          termList)));
        contents = add (contents, `HWeight (4, buttons));


                                // To translators: dialog label
        Wizard::SetContents (_("CA Selection"), contents, helptext, false, true);
        Wizard::SetNextButton(`next, Label::FinishButton() );
	UI::ChangeWidget (`id (`abort), `Enabled, false);

	list<string> firstEntry = (list<string>) CAList[0]:["",""];
	CaMgm::currentCA = firstEntry[0]:"";
					  
	UI::ChangeWidget (`id (`tree), `CurrentItem, CaMgm::currentCA);
			  
        symbol ui = nil;

        repeat
        {
            boolean anyitems = UI::QueryWidget (`id (`tree), `CurrentItem) != nil;
            UI::ChangeWidget (`id (`enter), `Enabled, anyitems);
            UI::ChangeWidget (`id (`delete), `Enabled, anyitems);	    

            ui = (symbol) UI::UserInput ();
            if (contains ([`enter,`delete], ui)
		&& anyitems)
            {
                CaMgm::currentCA = (string) UI::QueryWidget (`id (`tree), `CurrentItem);
		if (ui == `enter)
		{
		    // checking password
		    if (getPassword(CaMgm::currentCA) == nil)
		    {
			ui = `again;
		    }
		}
		if (ui == `delete)
		{
		    deleteCurrentCA ();
		}
            }
	    if (ui == `import)
	    {
		importCAFromDisk();  
	    }	    
        }
        until (contains ([`createRoot, `enter, `next, `import, `delete], ui));

        return ui;
    }

}
