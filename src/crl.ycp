/**
 *
 * File:
 *   crl.ycp
 *
 * Module:
 *   CA Management
 *
 * Summary:
 *   
 *
 * Authors:
 *   Stefan Schubert <schubi@suse.de>
 *
 * $Id$
 *
 * CRL of a selected CA
 *
 */


{

    textdomain "ca-management";

    import "CaMgm";
    import "Wizard";
    import "Label";
    import "Popup";
    import "YaPI::CaManagement";

    include "ca-management/util.ycp";

    /**
     * getDescription - CRL description
     * @param CA name
     * @return `again
     */
    
    define symbol createCRL(string CAname)``{

	// asking user
	UI::OpenDialog (`opt(`decorated ),
			`HBox( `HSpacing(2),
			       `VBox (
				      `VSpacing (1),
				      // popup window header
				      `Heading (_("Generate New CRL")),
				      `VSpacing (1),
				      `IntField( `id (`entry), _("&Valid to (days):"), 1, 10000, 30),
				      `VSpacing (1),					  
				      `HBox (  // push button label
					     `PushButton (`id(`ok), `opt(`default, `key_F10), Label::OKButton()),
					     `HStretch(),
					     `PushButton (`id(`cancel), `opt( `key_F9), Label::AbortButton())
					     ),
				      `VSpacing (1)					  
				      ),
			       `HSpacing (2)
			       )
			);

	UI::SetFocus (`id(`entry));
	symbol ui = nil;
	repeat
 	{
	    ui = (symbol) UI::UserInput ();
	    integer days = (integer) UI::QueryWidget(`id(`entry), `Value);
	    if (ui == `ok)
	    {
		// generating CRL
		boolean ret = nil;
		ret = (boolean) YaPI::CaManagement::AddCRL ($["caName"  : CAname,
							"caPasswd": getPassword(CaMgm::currentCA),
							"days"    : tostring(days)]);
		if (ret == nil
		    || ret == false)
		{
		    showErrorCaManagement ();
		}
	    }
	}
	until (contains ([`ok, `cancel], ui));
	UI::CloseDialog ();
	
	return `again;
    }


    /**
     * showLongCRLDescription - description of a CRL in textform
     * @param CA name
     */
    define string showLongCRLDescription (string CAname) ``{

	string ret = (string) YaPI::CaManagement::ReadCRL ($["caName":CAname,
							     "type":"plain"]);
	
	y2milestone("ReadCRL(%1): %3", CAname, ret);
	ret = "<pre>" + ret + "</pre>";
	
        if (ret == nil)
        {
            showErrorCaManagement ();
        }
	else
	{
	    UI::OpenDialog (`opt(`decorated ),
			    `HBox( `VSpacing(16),
				   `VBox (
					  `HSpacing (100),
					  // popup window header
					  `Heading (_("Description")),
					  `VSpacing (0.5),
					  `RichText (ret),
					  `VSpacing (1.5),
					  // push button label
					  `PushButton (`id(`ok), `opt(`default, `key_F10), Label::OKButton())
					  )
				   )
			    );

	    UI::SetFocus (`id(`ok));
	    UI::UserInput ();
	    UI::CloseDialog ();	    
	}	
    }

    /**
     * getDescription - CRL description
     * @param CA name
     * @return a string with the CRL description
     */
    define string getCRLDescription (string CAname) ``{
	map ret = (map) YaPI::CaManagement::ReadCRL ($["caName":CAname, "type":"parsed"]);
        if (ret == nil)
        {
	    map<string,any> messageMap = YaPI::Error();
	    string message = messageMap["summary"]:"";
	    string description = messageMap["description"]:"";
	    if (size (message) > 0)
	    {
		string retString = "\n";
		retString = retString + message + "\n";
		retString = retString + description;
		return retString;
	    }
        }
	y2milestone("ReadCRL(%1): %2", CAname, ret);

	string text = _("<b>Certificate Revocation List (CRL):</b>");
	text = text + "<pre>";
	text = text + "\nVersion: " + ret["VERSION"]:"";
	text = text + "\nSignature Algorithmus: " + ret["SIGNATURE_ALGORITHM"]:"";
	text = text + "\nIssuer: " + ret["ISSUER"]:"";
	text = text + _("\n\nLast Update: ") + ret["LASTUPDATE"]:"";
	text = text + _("\nNext Update: ") + ret["NEXTUPDATE"]:"";
	integer counter = 0;
	foreach (map element, ret["REVOKED_PARSED"]:[], ``{
	    if (counter == 0)
	    {
		text = text + _("\n\nRevoked Certificates: ");
	    }
	    counter = counter +1;
	    string reason = element["REASON"]:"";
	    reason = deletechars (reason, "\n");
	    text = text + "\n     Serial Number: " + element["SERIAL"]:"";
	    text = text + "\n            Date:   " + element["DATE"]:"";
	    text = text + "\n            Reason: " + reason;
	});
	
	return text;
	
    }

    /**
     * CRL Overview 
     * @return  'back, 'abort
     */
    define symbol crl () ``{

	integer i = 0;
	
        // help text 1/4
        string helptext = _("<p>Here, see the most important values of the CRL.</p>");
        // help text 2/4
        helptext = helptext + _("<p>With <b>Generate CRL</b>, a new CRL will be generated.</p>");
        // help text 3/4
        helptext = helptext + _("<p><b>View</b> shows a complete description.</p>");	
        // help text 4/4
        helptext = helptext + _("<p>You can <b>Export</b> the CRL to a file or LDAP Directory.</p>");

        term contents = `VBox ();
        contents = add (contents, `RichText(`id(`textinfo),""));
        term buttons = `HBox ();
        // To translators: pushbutton label
        buttons = add (buttons, `PushButton (`id (`gererateCRL), _("&Generate CRL")));
	buttons = add (buttons, `PushButton (`id (`view) , _("View")));	
	buttons = add (buttons, `HStretch());
	buttons = add (buttons, `MenuButton( _("Export"),
					      [
					       `item(`id(`exportFile), _("to &File") ),
					       `item(`id(`exportLDAP), _("to &LDAP"))
					       ]
					      ));
	
	contents = add (contents, buttons);

                                // To translators: dialog label
        Wizard::SetContents ( _("Certificate Revocation List (CRL) for CA: ") + CaMgm::currentCA, contents, helptext, true, true);
        UI::ChangeWidget( `id(`textinfo), `Value, getCRLDescription (CaMgm::currentCA));

        symbol ui = nil;

        repeat
        {
            ui = (symbol) UI::UserInput ();
	    if (ui == `gererateCRL)
	    {
		ui = createCRL (CaMgm::currentCA);
	    }
	    if (ui == `view)
	    {
		showLongCRLDescription (CaMgm::currentCA);
	    }	    	
	    if (ui == `exportLDAP)
	    {
		exportToLDAP ("CRL", CaMgm::currentCA, "" ,"", "");
	    }
	    if (ui == `exportFile)
	    {
		exportCRLtoFile (CaMgm::currentCA); 
	    }	    
        }
        until (contains ([`back, `abort, `createSubCA, `again, `next], ui));

        return ui;
    }

}
